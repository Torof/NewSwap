// SPDX-License-Identifier: SEE LICENSE IN LICENSE
pragma solidity 0.8.20;

import "./interfaces/IFactory.sol";
import {IPair} from "./interfaces/IPair.sol";
import {ICallee} from "./interfaces/ICallee.sol";
import {SafeERC20, IERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {ReentrancyGuard} from "@openzeppelin/contracts/utils/ReentrancyGuard.sol";
import {FixedPointMathLib} from "solady/utils/FixedPointMathLib.sol";
import {ERC20} from "solady/tokens/ERC20.sol";

// Heavily inspired by Uniswap v2, retailored to be updated to :
//  - Solidity 0.8.20
//  - flashloan standards {EIP-3156: FlashLoan}
//  - ERC20 standards using solady library and openZeppelin safeTransfer {EIP-20: ERC-20 Token Standard}
//  - No more SafeMath
//  - External fixed point math library {FixedPointMathLib} from solady
//  - actualized reentrancy guard {ReentrancyGuard} from OpenZeppelin
//  - stand alone swap function with integrated security checks, without flashloan integrated
//  - stand alone flashloan function
//  - assume users won't be using the router, so the swap function is integrated in the pair contract
//  - requires to custom errors

contract Pair is IPair, ReentrancyGuard, ERC20 {
    using SafeERC20 for IERC20;
    using FixedPointMathLib for uint256;

    uint public constant MINIMUM_LIQUIDITY = 10**3;
    address public immutable factory;
    address public token0;
    address public token1;
    uint112 public reserve0;
    uint112 public reserve1;
    uint32 public blockTimestampLast;
    uint public price0CumulativeLast;
    uint public price1CumulativeLast;
    uint public kLast;

    //TODO:  write some custom errors to replace requires

    event Burn(address indexed sender, uint amount0, uint amount1, address indexed to);
    event Mint(address indexed sender, uint amount0, uint amount1);
    
    event Swap(
        address indexed sender,
        uint amount0In,
        uint amount1In,
        uint amount0Out,
        uint amount1Out,
        address indexed to
    );

    event Sync(uint112 reserve0, uint112 reserve1);

    constructor() {
        factory = msg.sender;
    }

    function name() public override pure returns (string memory) {
        return "NewSwapPair";
    }

    function symbol() public override pure returns (string memory) {
        return "NSP";
    }

    //keep swap function to allow router integration
    function swap(uint amount0Out, uint amount1Out, address to, bytes calldata data) public nonReentrant() {
        require(amount0Out > 0 || amount1Out > 0, 'UniswapV2: INSUFFICIENT_OUTPUT_AMOUNT');
        (uint112 _reserve0, uint112 _reserve1,) = getReserves(); // gas savings
        require(amount0Out < _reserve0 && amount1Out < _reserve1, 'UniswapV2: INSUFFICIENT_LIQUIDITY');

        uint balance0;
        uint balance1;
        { // scope for _token{0,1}, avoids stack too deep errors
        address _token0 = token0;
        address _token1 = token1;
        require(to != _token0 && to != _token1, 'UniswapV2: INVALID_TO');
        if (amount0Out > 0) IERC20(_token0).safeTransfer(to, amount0Out); // optimistically transfer tokens
        if (amount1Out > 0) IERC20(_token1).safeTransfer(to, amount1Out); // optimistically transfer tokens
        if (data.length > 0) ICallee(to).newSwapCall(msg.sender, amount0Out, amount1Out, data);
        balance0 = IERC20(_token0).balanceOf(address(this));
        balance1 = IERC20(_token1).balanceOf(address(this));
        }
        uint amount0In = balance0 > _reserve0 - amount0Out ? balance0 - (_reserve0 - amount0Out) : 0;
        uint amount1In = balance1 > _reserve1 - amount1Out ? balance1 - (_reserve1 - amount1Out) : 0;
        require(amount0In > 0 || amount1In > 0, 'UniswapV2: INSUFFICIENT_INPUT_AMOUNT');
        { // scope for reserve{0,1}Adjusted, avoids stack too deep errors
        uint balance0Adjusted = balance0 * (1000) - (amount0In * 3);
        uint balance1Adjusted = balance1 *(1000) - (amount1In * 3);
        require(balance0Adjusted * (balance1Adjusted) >= uint(_reserve0) * (_reserve1) * (1000**2), 'UniswapV2: K');
        }

        _update(balance0, balance1, _reserve0, _reserve1);
        emit Swap(msg.sender, amount0In, amount1In, amount0Out, amount1Out, to);
    }

    //give amount of token out desired, get amount of token in
    //give address of token out, get address of token in
    //verify that the amount of token out is less than the reserve
    //0.3% fee on the amount of token in
    //take care of slippage : input desired amount of token out, get actual amount of token out and check if it is within slippage range
    function swapForTokenOut(address _tokenOut, uint _amountOut, uint _amountIn) public nonReentrant(){
        require(_tokenOut == token0 || _tokenOut == token1, "RS: INVALID_TOKEN_OUT");
        require(_amountOut > 0, "RS: INVALID_AMOUNT_OUT");
        address tokenOut;
        address tokenIn;
        if(_tokenOut == token0){
            tokenOut = token0;
            tokenIn = token1;
        } else {
            tokenOut = token1;
            tokenIn = token0;
        }
        require(IERC20(tokenOut).balanceOf(address(this)) >= _amountOut, "RS: INSUFFICIENT_BALANCE");

    }

    function skim(address to) external nonReentrant() {
        address _token0 = token0; // gas savings
        address _token1 = token1; // gas savings
        IERC20(_token0).safeTransfer(to, IERC20(_token0).balanceOf(address(this)) -(reserve0));
        IERC20(_token1).safeTransfer(to, IERC20(_token1).balanceOf(address(this)) - (reserve1));
    }

    function sync() external {
         _update(IERC20(token0).balanceOf(address(this)), IERC20(token1).balanceOf(address(this)), reserve0, reserve1);
    }

    function initialize(address _token0, address _token1) external {
        require(msg.sender == factory, "MS: FORBIDDEN"); // sufficient check
        token0 = _token0;
        token1 = _token1;
    }

   /**
    create 2 version of a mint function that can be called as is, not through a router: 
       - Provide liquidity to give - get shares,
       - Provide amount of shares to mint - transfer liquidity
    msg.sender should have enough balance and enough approved amounts of token0 and token1
    */


    function mint(address to) external nonReentrant() returns (uint liquidity) {
        (uint112 _reserve0, uint112 _reserve1,) = getReserves(); // gas savings
        uint balance0 = IERC20(token0).balanceOf(address(this));
        uint balance1 = IERC20(token1).balanceOf(address(this));
        uint amount0 = balance0 - _reserve0;
        uint amount1 = balance1 - (_reserve1);

        bool feeOn = _mintFee(_reserve0, _reserve1);
        uint _totalSupply = totalSupply(); // gas savings, must be defined here since totalSupply can update in _mintFee
        if (_totalSupply == 0) {
            liquidity = (amount0 * amount1).sqrt() - (MINIMUM_LIQUIDITY);
           _mint(address(0), MINIMUM_LIQUIDITY); // permanently lock the first MINIMUM_LIQUIDITY tokens
        } else {
            liquidity = FixedPointMathLib.min(amount0 * (_totalSupply) / _reserve0, amount1 * (_totalSupply) / _reserve1);
        }
        require(liquidity > 0, 'UniswapV2: INSUFFICIENT_LIQUIDITY_MINTED');
        _mint(to, liquidity);

        _update(balance0, balance1, _reserve0, _reserve1);
        if (feeOn) kLast = uint(reserve0) * (reserve1); // reserve0 and reserve1 are up-to-date
        emit Mint(msg.sender, amount0, amount1);
    }


    function flashloan() external {}


    function burn(address to) external nonReentrant() returns (uint amount0, uint amount1) {
        (uint112 _reserve0, uint112 _reserve1,) = getReserves(); // gas savings
        address _token0 = token0;                                // gas savings
        address _token1 = token1;                                // gas savings
        uint balance0 = IERC20(_token0).balanceOf(address(this));
        uint balance1 = IERC20(_token1).balanceOf(address(this));
        uint liquidity = balanceOf(address(this));

        bool feeOn = _mintFee(_reserve0, _reserve1);
        uint _totalSupply = totalSupply(); // gas savings, must be defined here since totalSupply can update in _mintFee
        amount0 = liquidity * (balance0) / _totalSupply; // using balances ensures pro-rata distribution
        amount1 = liquidity * (balance1) / _totalSupply; // using balances ensures pro-rata distribution
        require(amount0 > 0 && amount1 > 0, 'UniswapV2: INSUFFICIENT_LIQUIDITY_BURNED');
        _burn(address(this), liquidity);
        IERC20(token0).safeTransfer(to, amount0);
        IERC20(token1).safeTransfer(to, amount1);
        balance0 = IERC20(_token0).balanceOf(address(this));
        balance1 = IERC20(_token1).balanceOf(address(this));

        _update(balance0, balance1, _reserve0, _reserve1);
        if (feeOn) kLast = uint(reserve0) *(reserve1); // reserve0 and reserve1 are up-to-date
        emit Burn(msg.sender, amount0, amount1, to);
    }

        // if fee is on, mint liquidity equivalent to 1/6th of the growth in sqrt(k)
    function _mintFee(uint112 _reserve0, uint112 _reserve1) private returns (bool feeOn) {
        address feeTo = IFactory(factory).feeTo();
        feeOn = feeTo != address(0);
        uint _kLast = kLast; // gas savings
        if (feeOn) {
            if (_kLast != 0) {
                uint rootK = (uint(_reserve0) * (_reserve1)).sqrt();
                uint rootKLast = (_kLast).sqrt();
                if (rootK > rootKLast) {
                    uint numerator = totalSupply() * (rootK - (rootKLast));
                    uint denominator = rootK * (5) + (rootKLast);
                    uint liquidity = numerator / denominator;
                    if (liquidity > 0) _mint(feeTo, liquidity);
                }
            }
        } else if (_kLast != 0) {
            kLast = 0;
        }
    }

    function getReserves() public view returns (uint112 reserve0_, uint112 reserve1_, uint32 blockTimestampLast_) {
        reserve0_ = reserve0;
        reserve1_ = reserve1;
        blockTimestampLast_ = blockTimestampLast;
    }

    //NatSpec for _update function
    /**
     * @notice Updates the reserves and, on the first call per block, price accumulators
     * @dev Throws if block timestamp equals `blockTimestampLast`
     * @param balance0 The reserve0 of the pair
     * @param balance1 The reserve1 of the pair
     * @param _reserve0 The reserve0 of the pair
     * @param _reserve1 The reserve1 of the pair
     */

    function _update(uint balance0, uint balance1, uint112 _reserve0, uint112 _reserve1) private {
        require(balance0 <= type(uint112).max && balance1 <= type(uint112).max, "MS: OVERFLOW");
        uint32 blockTimestamp = uint32(block.timestamp % 2**32);
        uint32 timeElapsed = blockTimestamp - blockTimestampLast; // overflow is desired
        
        if (timeElapsed > 0 && _reserve0 != 0 && _reserve1 != 0) {
            // * never overflows, and + overflow is desired
            price0CumulativeLast += uint(_reserve1).divWad(uint(_reserve0)) * timeElapsed;
            price1CumulativeLast += uint(_reserve0).divWad(uint( _reserve1)) * timeElapsed;
        }

        reserve0 = uint112(balance0);
        reserve1 = uint112(balance1);
        blockTimestampLast = blockTimestamp;
        emit Sync(reserve0, reserve1);
    }

}